# Copyright 2019 The Dawn Authors
# Copyright 2021 The WebNN-native Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("../../scripts/webnn_overrides_with_defaults.gni")

import("${dawn_root}/scripts/dawn_component.gni")
import("${webnn_root}/generator/webnn_generator.gni")

###############################################################################
# WebNN C++ wrapper
###############################################################################

webnn_json_generator("cpp_gen") {
  target = "cpp"
  outputs = [ "src/dawn/webnn_cpp.cpp" ]
}

source_set("cpp") {
  deps = [
    ":cpp_gen",
    "${webnn_root}/include/webnn:cpp_headers",
  ]
  sources = get_target_outputs(":cpp_gen")
}

###############################################################################
# webnn_proc
###############################################################################

webnn_json_generator("proc_gen") {
  target = "proc"
  outputs = [
    "src/dawn/webnn_proc.c",
    "src/dawn/webnn_thread_dispatch_proc.cpp",
  ]
}

dawn_component("webnn_proc") {
  DEFINE_PREFIX = "WNN"

  public_deps = [ "${webnn_root}/include/webnn:headers" ]
  deps = [ ":proc_gen" ]
  sources = get_target_outputs(":proc_gen")
  sources += [ "${webnn_root}/include/webnn/webnn_proc.h" ]
}

###############################################################################
# Other generated files (upstream header, emscripten header, emscripten bits)
###############################################################################
webnn_json_generator("emscripten_bits_gen") {
  target = "emscripten_bits"
  outputs = [
    "emscripten-bits/library_webnn_enum_tables.js",
    "emscripten-bits/webnn.h",
    "emscripten-bits/webnn_cpp.cpp",
    "emscripten-bits/webnn_cpp.h",
    "emscripten-bits/webnn_struct_info.json",
  ]
}
