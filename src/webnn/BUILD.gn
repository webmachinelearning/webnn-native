# Copyright 2019 The Dawn Authors
# Copyright 2021 The WebNN-native Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//third_party/dawn/scripts/dawn_overrides_with_defaults.gni")

import("${dawn_root}/scripts/dawn_component.gni")
import("${webnn_root}/generator/webnn_generator.gni")

###############################################################################
# WebNN headers
###############################################################################

webnn_json_generator("webnn_headers_gen") {
  target = "webnn_headers"
  outputs = [
    "src/include/webnn/webnn_proc_table.h",
    "src/include/webnn/webnn.h",
  ]
}

webnn_json_generator("emscripten_bits_gen") {
  target = "emscripten_bits"
  outputs = [
    "src/webnn/webnn_struct_info.json",
    "src/webnn/library_webnn_enum_tables.js",
  ]
}

source_set("webnn_headers") {
  all_dependent_configs =
      [ "${webnn_root}/src/common:dawn_public_include_dirs" ]
  public_deps = [ ":webnn_headers_gen" ]

  sources = get_target_outputs(":webnn_headers_gen")
}

###############################################################################
# WebNN C++ headers
###############################################################################

webnn_json_generator("webnncpp_headers_gen") {
  target = "webnncpp_headers"
  outputs = [ "src/include/webnn/webnn_cpp.h" ]
}

source_set("webnncpp_headers") {
  public_deps = [
    ":webnn_headers",
    ":webnncpp_headers_gen",
  ]

  sources = get_target_outputs(":webnncpp_headers_gen")
  sources += [ "${webnn_root}/src/include/webnn/EnumClassBitmasks.h" ]
}

###############################################################################
# WebNN C++ wrapper
###############################################################################

webnn_json_generator("webnncpp_gen") {
  target = "webnncpp"
  outputs = [ "src/webnn/webnn_cpp.cpp" ]
}

source_set("webnncpp") {
  deps = [
    ":webnncpp_gen",
    ":webnncpp_headers",
  ]
  sources = get_target_outputs(":webnncpp_gen")
}

###############################################################################
# webnn_proc
###############################################################################

webnn_json_generator("webnn_proc_gen") {
  target = "webnn_proc"
  outputs = [ "src/webnn/webnn_proc.c" ]
}

dawn_component("webnn_proc") {
  DEFINE_PREFIX = "WEBNN"

  public_deps = [ ":webnn_headers" ]
  deps = [ ":webnn_proc_gen" ]
  sources = get_target_outputs(":webnn_proc_gen")
  sources += [ "${webnn_root}/src/include/webnn/webnn_proc.h" ]
}
